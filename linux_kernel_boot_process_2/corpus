mm_s OMIT
+---------------+ <------------------------------------------------------------+
|               |                                                              |
| protected-mode|                                                              |
|   kernel      |                                                              |
+---------------+0x100000 <--------------------+                               |
|               |                              |                               |
|               |                              |                               |
|               |                              |                               |
+---------------+X+setup_code_size <----------+|                               |
| kernel setup  |                             ||                               |
|               |                             ||                               |
+---------------+X  <------------+            ||                               |
| Boot loader   |                |            ||                               |
+---------------+0x1000          |            ||                               |
| reserved      |                +------------++-------------------------------+
+---------------+0x0800          |header+setup|       compressed kernel        |
| used by MBR   |                +------------+--------------------------------+
+---------------+0x0600
| BIOS use only |
+---------------+0x0000
mm_e OMIT

header_1_s OMIT
__HEAD
.code32
ENTRY(startup_32)
/*
 * 32bit entry is 0 and it is ABI so immutable!
 * If we come here directly from a bootloader,
 * kernel(text+data+bss+brk) ramdisk, zero_page, command line
 * all need to be under the 4G limit.
 */
cld
header_1_e OMIT

header_2_s OMIT
/*
 * Test KEEP_SEGMENTS flag to see if the bootloader is asking
 * us to not reload segments
 */
testb $KEEP_SEGMENTS, BP_loadflags(%esi)
jnz 1f

cli
movl	$(__BOOT_DS), %eax
movl	%eax, %ds
movl	%eax, %es
movl	%eax, %ss
header_2_e OMIT

header_3_s OMIT
leal	(BP_scratch+4)(%esi), %esp
call	1f
1:	popl	%ebp
subl	$1b, %ebp
header_3_e OMIT

get_ip_s OMIT
+----------+ <-esp      +--------------------------+
|  empty   |            |runtime address of label 1|
|  stack   | -------->  +--------------------------+ <-esp
|          | after call |                          |
+----------+            +--------------------------+
get_ip_e OMIT

header_4_s OMIT
/* setup a stack and make sure cpu supports long mode. */
	movl	$boot_stack_end, %eax
	addl	%ebp, %eax
	movl	%eax, %esp
	...
/*
 * Stack and heap for uncompression
 */
	.bss
	.balign 4
...
boot_stack:
	.fill BOOT_STACK_SIZE, 1, 0
boot_stack_end:
header_4_e OMIT

header_5_s OMIT
call	verify_cpu
testl	%eax, %eax
jnz	no_longmode
header_5_e OMIT

header_6_s OMIT
#ifdef CONFIG_RELOCATABLE
	movl	%ebp, %ebx
	movl	BP_kernel_alignment(%esi), %eax
	decl	%eax
	addl	%eax, %ebx
	notl	%eax
	andl	%eax, %ebx
	cmpl	$LOAD_PHYSICAL_ADDR, %ebx
	jge	1f
#endif
	movl	$LOAD_PHYSICAL_ADDR, %ebx
header_6_e OMIT

header_7_s OMIT
/* Target address to relocate to for decompression */
movl	BP_init_size(%esi), %eax
subl	$_end, %eax
addl	%eax, %ebx
header_7_e OMIT

relocation_s OMIT

         |<----------------  init_size ----------------------------------->|
         |                                                                 |
   relocation address                 |<----------- _end ----------------->|
         |                            |                                    |
         v                            |                                    v
+--------+----------------------------+------------------------------------+
|        | uncompressed kernel        | decompress code + compressed kernel|
+--------+----------------------------+------------------------------------+
0                                     ^                                    ^
                                      |                                    |
         +----------------------------+       +----------------------------+
         |                                    |
         |                                    |
         |                                    |
+--------+------------------------------------+----------------------------+
|        | decompress code + compressed kernel|                            |
+--------+------------------------------------+----------------------------+
0        ^
         |
     0x100000
relocation_e OMIT

compressed_s OMIT
+--------------+<- 0, _head
| .head.text   |
+--------------+<- _ehead
| .rodata..    |
| compressed   |
|              |
| (compressed- |
|  kernel)     |
+--------------+<- _text
| .text        |
+--------------+<- _rodata, _etext
| .rodata      |
+--------------+<- _got, _erodata
| .got         |
+--------------+<- _data, _egot
| .data        |
+--------------+<- _bss, _edata
| .bss         |
+--------------+<- _pgtable, _ebss
| .pgtable     |
| (x86_64 only)|
+--------------+<- _epgtable, _end
compressed_e OMIT

header_8_s OMIT
/* Load new GDT with the 64bit segments using 32bit descriptor */
addl	%ebp, gdt+2(%ebp)
lgdt	gdt(%ebp)
...
	.data
gdt:
	.word	gdt_end - gdt
	.long	gdt
	.word	0
	.quad	0x0000000000000000	/* NULL descriptor */
	.quad	0x00af9a000000ffff	/* __KERNEL_CS */
	.quad	0x00cf92000000ffff	/* __KERNEL_DS */
	.quad	0x0080890000000000	/* TS descriptor */
	.quad   0x0000000000000000	/* TS continued */
gdt_end:
header_8_e OMIT

header_9_s OMIT
/* Enable PAE mode */
movl	%cr4, %eax
orl	$X86_CR4_PAE, %eax
movl	%eax, %cr4
header_9_e OMIT

header_10_s OMIT
 /*
  * Build early 4G boot pagetable
  */
	/* Initialize Page tables to 0 */
	leal	pgtable(%ebx), %edi
	xorl	%eax, %eax
	movl	$(BOOT_INIT_PGT_SIZE/4), %ecx
	rep	stosl

	/* Build Level 4 */
	leal	pgtable + 0(%ebx), %edi
	leal	0x1007 (%edi), %eax
	movl	%eax, 0(%edi)

	/* Build Level 3 */
	leal	pgtable + 0x1000(%ebx), %edi
	leal	0x1007(%edi), %eax
	movl	$4, %ecx
1:	movl	%eax, 0x00(%edi)
	addl	$0x00001000, %eax
	addl	$8, %edi
	decl	%ecx
	jnz	1b

	/* Build Level 2 */
	leal	pgtable + 0x2000(%ebx), %edi
	movl	$0x00000183, %eax
	movl	$2048, %ecx
1:	movl	%eax, 0(%edi)
	addl	$0x00200000, %eax
	addl	$8, %edi
	decl	%ecx
	jnz	1b

	/* Enable the boot page tables */
	leal	pgtable(%ebx), %eax
	movl	%eax, %cr3
header_10_e OMIT

header_11_s OMIT
.section ".pgtable","a",@nobits
.balign 4096
pgtable:
.fill BOOT_PGT_SIZE, 1, 0

# define BOOT_INIT_PGT_SIZE	(6*4096)
header_11_e OMIT

pgtable_s OMIT
  pgtable+6*4096 +---------+
                 |pmde 2047|
                 +---------+
                 | pmdt-   |
                 | entry   |
                 | ...     |
                 +---------+
                 |pmde 1536|
  pgtable+5*4096 +---------+<----------+
                 |pmde 1535|           |
                 +---------+           |
                 | pmdt-   |           |
                 | entry   |           |
                 | ...     |           |
                 +---------+           |
                 |pmde 1024|           |
  pgtable+4*4096 +---------+<-------+  |
                 |pmde 1023|        |  |
                 +---------+        |  |
                 | pmdt-   |        |  |
                 | entry   |        |  |
                 | ...     |        |  |
                 +---------+        |  |
                 |pmde 512 |        |  |
  pgtable+3*4096 +---------+<----+  |  |
                 |pmde 511 |     |  |  |
                 +---------+     |  |  |
                 | pmdt-   |     |  |  |
                 | entry   |     |  |  |
                 | ...     |     |  |  |
                 +---------+     |  |  |
                 | pmde 1  |     |  |  |
                 +---------+     |  |  |
                 | pmde 0  |     |  |  |
  pgtable+2*4096 +---------+<-+  |  |  |
                 | pudt-   |  |  |  |  |
                 | entry   |  |  |  |  |
                 | ...     |  |  |  |  |
                 +---------+  |  |  |  |
                 | pude 3  +--+--+--+--+
                 +---------+  |  |  |
                 | pude 2  +--+--+--+
                 +---------+  |  |
                 | pude 1  +--+--+
                 +---------+  |
                 | pude 0  +--+
    pgtable+4096 +---------+<---+
                 | pgdt-   |    |
                 | entry   |    |
                 | ...     |    |
                 +---------+    |
                 | pgde 0  +----+
       pgtable   +---------+
pgtable_e OMIT

header_12_s OMIT
/* Enable Long mode in EFER (Extended Feature Enable Register) */
movl	$MSR_EFER, %ecx
rdmsr
btsl	$_EFER_LME, %eax
wrmsr
header_12_e OMIT

header_13_s OMIT
pushl	$__KERNEL_CS
leal	startup_64(%ebp), %eax
/* Enter paged protected Mode, activating Long Mode */
movl	$(X86_CR0_PG | X86_CR0_PE), %eax /* Enable Paging and Protected mode */
movl	%eax, %cr0
/* Jump from 32bit compatibility mode into 64bit mode. */
lret
...
	.code64
	.org 0x200
ENTRY(startup_64)
header_13_e OMIT

long_mode_1_s OMIT
/* Setup data segments. */
xorl	%eax, %eax
movl	%eax, %ds
movl	%eax, %es
movl	%eax, %ss
movl	%eax, %fs
movl	%eax, %gs
long_mode_1_e OMIT

long_mode_2_s OMIT
/* Zero EFLAGS */
pushq	$0
popfq
long_mode_2_e OMIT

long_mode_3_s OMIT
/*
 * Copy the compressed kernel to the end of our buffer
 * where decompression in place becomes safe.
 */
	pushq	%rsi
	leaq	(_bss-8)(%rip), %rsi
	leaq	(_bss-8)(%rbx), %rdi
	movq	$_bss /* - $startup_32 */, %rcx
	shrq	$3, %rcx
	std
	rep	movsq
	cld
	popq	%rsi
long_mode_3_e OMIT

long_mode_4_s OMIT
/*
 * Jump to the relocated address.
 */
	leaq	relocated(%rbx), %rax
	jmp	*%rax
...
	.text
relocated:
long_mode_4_e OMIT

long_mode_5_s OMIT
/*
 * Clear BSS (stack is currently empty)
 */
	xorl	%eax, %eax
	leaq    _bss(%rip), %rdi
	leaq    _ebss(%rip), %rcx
	subq	%rdi, %rcx
	shrq	$3, %rcx
	rep	stosq
long_mode_5_e OMIT

long_mode_6_s OMIT
/*
 * Adjust our own GOT
 */
	leaq	_got(%rip), %rdx
	leaq	_egot(%rip), %rcx
1:
	cmpq	%rcx, %rdx
	jae	2f
	addq	%rbx, (%rdx)
	addq	$8, %rdx
	jmp	1b
long_mode_6_e OMIT

long_mode_7_s OMIT
/*
 * Do the extraction, and jump to the new kernel..
 */
	pushq	%rsi			/* Save the real mode argument */
	movq	%rsi, %rdi		/* real mode address */
	leaq	boot_heap(%rip), %rsi	/* malloc area for uncompression */
	leaq	input_data(%rip), %rdx  /* input_data */
	movl	$z_input_len, %ecx	/* input_len */
	movq	%rbp, %r8		/* output target address */
	movq	$z_output_len, %r9	/* decompressed length, end of relocs */
	call	extract_kernel		/* returns kernel location in %rax */
	popq	%rsi
long_mode_7_e OMIT

long_mode_8_s OMIT
boot_heap:
	.fill BOOT_HEAP_SIZE, 1, 0
...
#ifdef CONFIG_KERNEL_BZIP2
# define BOOT_HEAP_SIZE		0x400000
#else /* !CONFIG_KERNEL_BZIP2 */
# define BOOT_HEAP_SIZE		 0x10000
#endif
long_mode_8_e OMIT

piggy_s OMIT
.section ".rodata..compressed","a",@progbits
.globl z_input_len
z_input_len = 1777261
.globl z_output_len
z_output_len = 9491880
.globl input_data, input_data_end
input_data:
.incbin "arch/x86/boot/compressed/vmlinux.bin.gz"
input_data_end:
piggy_e OMIT

long_mode_9_s OMIT
...
choose_random_location((unsigned long)input_data, input_len,
			(unsigned long *)&output,
			max(output_len, kernel_total_size),
			&virt_addr);
long_mode_9_e OMIT

long_mode_10_s OMIT
debug_putstr("\nDecompressing Linux... ");
__decompress(input_data, input_len, NULL, NULL, output, output_len,
		NULL, error);
long_mode_10_e OMIT

pt_load_s OMIT
static void parse_elf(void *output)
{
	...
	for (i = 0; i < ehdr.e_phnum; i++) {
		phdr = &phdrs[i];

		switch (phdr->p_type) {
		case PT_LOAD:
#ifdef CONFIG_RELOCATABLE
			dest = output;
			dest += (phdr->p_paddr - LOAD_PHYSICAL_ADDR);
#else
			dest = (void *)(phdr->p_paddr);
#endif
			memmove(dest, output + phdr->p_offset, phdr->p_filesz);
			break;
		default: /* Ignore other PT_* */ break;
		}
	}
	...
}
pt_load_e OMIT

reloc_s OMIT
static void handle_relocations(void *output, unsigned long output_len,
			       unsigned long virt_addr)
{
	...
	/*
	 * Calculate the delta between where vmlinux was linked to load
	 * and where it was actually loaded.
	 */
	delta = min_addr - LOAD_PHYSICAL_ADDR;
	...
	for (reloc = output + output_len - sizeof(*reloc); *reloc; reloc--) {
		...
		*(uint32_t *)ptr += delta;
	}
}
reloc_e OMIT

long_mode_11_s OMIT
asmlinkage __visible void *extract_kernel(void *rmode, memptr heap,
				  unsigned char *input_data,
				  unsigned long input_len,
				  unsigned char *output,
				  unsigned long output_len)
{
	...
	return output;
}

// header_64.S
	call	extract_kernel		/* returns kernel location in %rax */
/*
 * Jump to the decompressed kernel.
 */
	jmp	*%rax
long_mode_11_e OMIT
